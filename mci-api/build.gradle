apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'war'
apply plugin: 'rpm'
apply plugin: 'idea'

mainClassName = "org.sharedhealth.mci.web.launch.Application"

repositories {
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    flatDir {
        dirs "${rootDir}/lib"
    }
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    compile libraries.spark_core,libraries.apache_log4j_core,libraries.cassandra_driver_core,
            libraries.cassandra_driver_mapping, libraries.cassandra_all,libraries.hapi_fhir_base,
            libraries.hapi_fhir_structures,libraries.hapi_fhir_validation,
            libraries.apache_collections, libraries.jackson_core,libraries.jackson_annotations,libraries.jackson_databind,
            libraries.hector_core,libraries.ehcache_core

    testCompile testLibraries.junit,testLibraries.mockito,testLibraries.cassandra_unit,testLibraries.system_rules,
            testLibraries.mutagen, testLibraries.mutagen_cassandra, testLibraries.wiremock
}

test {
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed"
    }

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
    // set heap size for the test JVM(s)
    minHeapSize = "512m"
    maxHeapSize = "1024m"
}

configurations {
    migration {
        extendsFrom compile
    }
    all*.exclude group: 'org.slf4j', module: "log4j-over-slf4j"
    all*.exclude group: 'ch.qos.logback', module: "logback-classic"
    all*.exclude group: 'ch.qos.logback', module: "logback-core"
}

jar {
    baseName = 'mci-registry'
    version = '1.0-SNAPSHOT'
}

private Properties loadConfig() {
    Properties properties = new Properties()
    properties.load(new FileInputStream(file("${projectDir}/../env/local.properties")));
    properties
}

task assembly(dependsOn: 'build') << {
    new File("${buildDir}/etc/").mkdir();
}

task exportProperties(dependsOn: 'assembly') << {
    PrintStream output = new PrintStream(new FileOutputStream(file("${buildDir}/etc/mci-registry")));
    Properties properties = loadConfig()
    properties.each { prop, val ->
        output.println("export " + prop + "=" + val)
    }
}

task dist(dependsOn: ['exportProperties'],
        type: Rpm) {
    packageName = 'mci-registry'
    version = '1.0'
    release = project.hasProperty('release') ? project.release : '1'
    arch = NOARCH
    os = LINUX

    into '/opt/mci-registry'

    postInstall = file('scripts/utils/postInstall.sh')
    preUninstall = file('scripts/utils/preUninstall.sh')
    postUninstall = file('scripts/utils/postUninstall.sh')

    from("${buildDir}/etc") {
        fileMode = 0755
        createDirectoryEntry true
        into 'etc'
    }

    from("scripts/rpm") {
        fileMode = 0755
        createDirectoryEntry true
        exclude 'placeholder'
        into 'bin'
    }

    from("${buildDir}/libs") {
        fileMode = 0755
        createDirectoryEntry true
        into 'lib'
    }

    from("scripts/rpm") {
        fileMode = 0755
        createDirectoryEntry true
        exclude 'mci-registry'
        exclude 'start.sh'
        into 'var'
    }
}

war {
    /*should be removed once we start using as web-server*/
    from "$buildDir/classes/main"
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    /*should be removed once we start using as web-server*/
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
    baseName = 'mci-registry'
    from sourceSets.main.resources
    manifest {
        attributes('Main-Class': mainClassName)
    }
}

task runMCI(type: JavaExec) {
    main = mainClassName
    classpath sourceSets.main.runtimeClasspath
    environment(loadConfig())
}

task debugMCI(type: JavaExec) {
    main = mainClassName
    classpath sourceSets.main.runtimeClasspath
    environment(loadConfig())
    debug = true
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }
    dependencies {
        classpath(
                'com.netflix.nebula:gradle-ospackage-plugin:2.2.+'
        )
    }
}
