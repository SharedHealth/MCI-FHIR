apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'war'
apply plugin: 'rpm'
apply plugin: 'idea'

mainClassName = "org.sharedhealth.mci.web.launch.Main"

repositories {
    mavenCentral()
    flatDir {
        dirs "${rootDir}/lib"
    }
}

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

dependencies {
    testCompile testLibraries.junit
}

test {
    testLogging {
        exceptionFormat "full" // default is "short"
        events "passed", "skipped", "failed"
    }

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
    // set heap size for the test JVM(s)
    minHeapSize = "512m"
    maxHeapSize = "1024m"

    // set JVM arguments for the test JVM(s)
    jvmArgs '-XX:MaxPermSize=256m'
}

configurations {
    migration {
        extendsFrom compile
    }
}

jar {
    baseName = 'mci-api'
    version = '2.5-SNAPSHOT'
}

private Properties loadConfig() {
    Properties properties = new Properties()
    properties.load(new FileInputStream(file("${projectDir}/../env/local.properties")));
    properties
}

task assembly(dependsOn: 'build') << {
    new File("${buildDir}/etc/").mkdir();
}

task exportProperties(dependsOn: 'assembly') << {
    PrintStream output = new PrintStream(new FileOutputStream(file("${buildDir}/etc/mci")));
    Properties properties = loadConfig()
    properties.each { prop, val ->
        output.println("export " + prop + "=" + val)
    }
}

task dist(dependsOn: ['exportProperties'], type: Rpm) {
    packageName = 'mci'
    version = '2.5'
    release = project.hasProperty('release') ? project.release : '1'
    arch = NOARCH
    os = LINUX

    into '/opt/mci'

    postInstall = file('scripts/utils/postInstall.sh')
    preUninstall = file('scripts/utils/preUninstall.sh')
    postUninstall = file('scripts/utils/postUninstall.sh')

    from("${buildDir}/etc") {
        fileMode = 0755
        createDirectoryEntry true
        into 'etc'
    }

    from("scripts/rpm") {
        fileMode = 0755
        createDirectoryEntry true
        exclude 'placeholder'
        into 'bin'
    }

    from("${buildDir}/libs") {
        fileMode = 0755
        createDirectoryEntry true
        into 'lib'
    }

    from("scripts/rpm") {
        fileMode = 0755
        createDirectoryEntry true
        exclude 'mci'
        exclude 'start.sh'
        into 'var'
    }
}

war {
    from sourceSets.main.resources
    from "$buildDir/classes/main"
    manifest {
        attributes('Main-Class': mainClassName)
    }
}

task runMCI(type: JavaExec) {
    main = mainClassName
    classpath sourceSets.main.runtimeClasspath
    environment(loadConfig())
}

task debugMCI(type: JavaExec) {
    main = mainClassName
    classpath sourceSets.main.runtimeClasspath
    environment(loadConfig())
    debug = true
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath(
                'com.netflix.nebula:gradle-ospackage-plugin:2.2.+'
        )
    }
}
