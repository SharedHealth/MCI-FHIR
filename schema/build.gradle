apply plugin: 'java'
apply plugin: 'idea'

repositories {
    mavenCentral()
    flatDir {
        dirs "${rootDir}/lib"
    }
}
configurations {
    all *.exclude group: 'log4j', module: "log4j"
}

configurations.all {
    resolutionStrategy {
        force 'com.google.guava:guava:18.0', 'org.apache.httpcomponents:httpclient:4.3.5'
    }
}

def mainClassName = "org.sharedhealth.mci.web.Migrations"

jar {
    baseName = 'mci-registry-schema'
    version = '0.1-SNAPSHOT'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes('Main-Class': mainClassName)
    }
}


dependencies {
    compile libraries.mutagen, libraries.mutagen_cassandra, libraries.cassandra_driver_core, libraries.apache_lang
}

task runMigration(type: org.gradle.api.tasks.JavaExec) {
    Properties properties = new Properties();
    properties.load(new FileInputStream(file('../env/local.properties')))
    main = mainClassName
    jvmArgs = ['-DSHR_LOG_LEVEL=DEBUG']
    classpath = sourceSets.main.runtimeClasspath
    environment properties
}